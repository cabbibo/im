#pragma kernel Set
#pragma kernel Collision
#pragma kernel Constraint

#include "../Chunks/noise.cginc"
#include "../Chunks/snoise.cginc"
#include "../Chunks/curlNoise.cginc"


float4x4 transform; 

float _Delta;
float _Time;
uint _Pass;
float _SpringDistance;
float _FalloffRadius;

uint _NumVertsPerHair;
float _HairLength;
float _UpForce;
float _HairVariance;


float3 _WandPos;


Texture2D<float4> _HeightMap;
SamplerState sampler_HeightMap;
float _MapSize;
float _MapHeight;

float3 worldPos( float3 pos ){
    float4 c = _HeightMap.SampleLevel(sampler_HeightMap, pos.xz * _MapSize , 1);//tex2Dlod(_HeightMap , float4(pos.xz * _MapSize,0,0) );
    pos.y =  c.x * _MapHeight + 1;
    return pos;
}

float grassHeight( float3 pos ){
    float4 c = _HeightMap.SampleLevel(sampler_HeightMap, pos.xz * _MapSize , 1);//tex2Dlod(_HeightMap , float4(pos.xz * _MapSize,0,0) );
    return c.a;
}



float3 directionVector( float3 pos ){
    float4 c = _HeightMap.SampleLevel(sampler_HeightMap, pos.xz * _MapSize , 1);//tex2Dlod(_HeightMap , float4(pos.xz * _MapSize,0,0) );
    return 2*float3(c.g,0,c.b)-1;
}


struct Vert{
  float3 pos;
  float3 oPos;
  float3 nor;
  float3 tang;
  float2 uv;
  float2 debug;
};


/*struct Base{
  float3 pos;
  float3 vel;
  float3 nor;
  float3 tangent;
  float2 uv;
  float2 debug;
};*/


struct Base{
  float3 pos;
  float3 vel;
  float3 nor;
  float3 tang;
  float2 uv;
  float used;
  float3 triIDs;
  float3 triWeights;
  float3 debug;
};



float _NormalForce;
float _NoiseForce;
float _NoiseSpeed;
float _NoiseSize;
float _Dampening;
float3 _Player;

float _PushRadius;
float _PushForce;

RWStructuredBuffer<Vert> _VertBuffer;
RWStructuredBuffer<Base> _BaseBuffer;

uint _VertBuffer_COUNT;
uint _BaseBuffer_COUNT;

#include "../Chunks/safeID.cginc"

float3 getVel( float3 p1 , float3 p2 ){

  float3 v = p1 - p2;

  if( length( v ) > .5){ v = normalize( v ) *.5; }
  return v;
  
}

float3 springForce( float3 p1 , float3 p2 , float sl ){
  float3 dif = p1 - p2;
  float l = length(dif);

  if( l > 0 ){
    float d = l - sl;
    return normalize( dif ) * d;
  }else{
    return float3(0,0,0);
  }

}

float getLength( int bID ){
  return ((sin(float(bID)*10000)+ _HairVariance) / _HairVariance ) * _HairLength;
}
#define NR_THREADS 128

[numthreads(NR_THREADS,1,1)]
void Set (uint threadID : SV_GroupIndex, uint3 u3GroupID : SV_GroupID, uint3 id : SV_DispatchThreadID) {


  uint pID = threadID + u3GroupID.x * NR_THREADS;
  uint bID = pID / _NumVertsPerHair;
  if( pID < _VertBuffer_COUNT && bID < _BaseBuffer_COUNT ){


    pID = safeID( pID , _VertBuffer_COUNT );
    Vert vert = _VertBuffer[pID];
    Base base = _BaseBuffer[safeID( bID , _BaseBuffer_COUNT )];

    float3 bP = base.pos;//+ float3(10000,0,0);//mul( transform,  float4(base.pos,1)).xyz;
    float3 bN = float3(0,1,0);//normalize(mul( transform,  float4(base.nor,0))).xyz;
    float3 s = float3(1,1,1);//mul( transform , float4(1,1,1,0)).xyz;

    float l = float(pID % _NumVertsPerHair ) / float(_NumVertsPerHair);

    vert.uv = float2( l , float(bID)/float(_BaseBuffer_COUNT));
    vert.debug.x = getLength(bID);
    vert.pos  = bP + bN * vert.uv.x * vert.debug.x;
    vert.oPos = vert.pos;//base.pos + base.nor * l + _HairLength;

    _VertBuffer[pID] = vert;

  }
  
}



[numthreads(NR_THREADS,1,1)]
void Collision (uint threadID : SV_GroupIndex, uint3 u3GroupID : SV_GroupID, uint3 id : SV_DispatchThreadID) {

  //int pID = id.x + id.y * gridX + id.z * gridX * gridY;

  uint pID = threadID + u3GroupID.x * NR_THREADS;
  uint bID = pID / _NumVertsPerHair;
  if( pID < _VertBuffer_COUNT && bID < _BaseBuffer_COUNT ){

    pID = safeID( pID , _VertBuffer_COUNT );
    bID = safeID( bID , _BaseBuffer_COUNT );
    Vert vert = _VertBuffer[pID];
    Base base = _BaseBuffer[bID];

    //if( base.used == 1 ){

    float3 bP = base.pos;//mul( transform,  float4(base.pos,1)).xyz;
    float3 bN = base.nor;//normalize(mul( transform,  float4(base.nor,0))).xyz;
    float3 s = float3(1,1,1);//mul( transform , float4(1,1,1,0)).xyz;

    float3 force = bN * _NormalForce;//float3( 0 , base.nor , 0.0 );

    force += curlNoise( vert.pos * _NoiseSize + float3(0,_Time*_NoiseSpeed,0) ) * _NoiseForce;
    force += float3(0,_UpForce*vert.uv.x,0);
    float2 pForce = _Player.xz-vert.pos.xz;//)/pow(length(_Player.xz-vert.pos.xz),4);
   float3 vec = directionVector( vert.pos );

   force += vec*(1+snoise(vert.pos* .3+float3(0,_Time,0)));



      pForce = _Player.xz-vert.pos.xz;
      float lerpSpeed = saturate((_PushRadius - length(pForce))/_PushRadius);
      force -= lerp(0,_PushForce  , lerpSpeed) *normalize(float3(pForce.x , 0 , pForce.y ));
      
      pForce = _WandPos.xz-vert.pos.xz;
      lerpSpeed = saturate((_PushRadius - length(pForce))/_PushRadius);
      force -= lerp(0,_PushForce  , lerpSpeed) *normalize(float3(pForce.x , 0 , pForce.y ));

    //force += .2*normalize((_HandL-vert.pos))/pow(length(_HandL-vert.pos),4);
    //force += .2*normalize((_HandR-vert.pos))/pow(length(_HandR-vert.pos),4);
    // Inertia: objects in motion stay in motion.
    float3 vel = getVel( vert.pos , vert.oPos);

    if( length( force ) > 10 ){
      force = normalize(force) * 10;
    }
 

    bool tb = false;

    if( length(vert.pos) == 0 ){
      vert.pos  = bP + bN * vert.uv.x * vert.debug.x;
      vert.oPos = vert.pos;
      tb = true;
    }

    if( length(vert.pos.x) == 1000 ){
      vert.pos  = bP + bN * vert.uv.x * vert.debug.x;
      vert.oPos = vert.pos;
      tb = true;
    }

    // If we are at the bottom, 
    if( vert.uv.x == 0 ){ 
      vert.pos = bP; 
      vert.oPos = bP;
      tb = true;
      vert.nor = bN;
    }else{

       Vert vDown = _VertBuffer[ safeID( pID - 1 , _VertBuffer_COUNT )];
       vert.nor = normalize(vert.pos - vDown.pos);

       vert.tang = normalize( cross( vert.nor , vert.pos - _Player ) ) ;
      //vert.pos = bP + bN * .1 *vert.uv.x; 
      //vert.oPos = bP+ bN * .1 *vert.uv.x; 
      //tb = true;

    }

    /*if( length(vert.pos) > .2 ){
      vert.pos = bP + bN * .1 * vert.uv.x; 
      vert.oPos = bP+ bN * .1 * vert.uv.x; 
      tb = true; 
    }

    if( length(vert.pos) < .1 ){
      vert.pos = bP + bN * .1 * vert.uv.x; 
      vert.oPos = bP+ bN * .1 * vert.uv.x; 
      tb = true; 
    }*/

    //vert.nor = normalize(vert.pos - vert.oPos);

    if( _Delta  < .1 && tb == false ){

      float dampening =_Dampening;//.99999;// - vert.life * .01;
      float3 next = vert.pos + vel * dampening + force * _Delta * _Delta;

      vert.oPos = vert.pos;
      vert.pos = next;

    }

    vert.debug.x = grassHeight(bP) * getLength(bID);
    _VertBuffer[pID] = vert;
 

  }
  
}





[numthreads(NR_THREADS,1,1)]
void Constraint(uint threadID : SV_GroupIndex, uint3 u3GroupID : SV_GroupID, uint3 id : SV_DispatchThreadID) {
  
  // Get the id using thread / group indices
  uint pID = threadID + u3GroupID.x * NR_THREADS ;

  // Since we are doing 2 passes for verlet, multiply by 2 
  // to get both the vertices!
  pID = pID * 2;
  pID = pID + _Pass;


float3 s = length(mul( transform , float4(1,1,1,0)).xyz)/3;
  if( pID < uint(_VertBuffer_COUNT) ){

    pID = uint(safeID(pID,_VertBuffer_COUNT));
    int pUp = uint(safeID(pID+1,_VertBuffer_COUNT));
    Vert v1 = _VertBuffer[pID];

    float n = sin(1000*float(pID/_NumVertsPerHair)); 

    // If we are are the end of the hair, no need to do verlet
    if( v1.uv.x < 1-(1/ float(_NumVertsPerHair))){
      Vert v2 = _VertBuffer[pUp];




      float3 dif = v1.pos - v2.pos;


      float l = length( dif );// * ((4+n)/4);

      float difference = 0;
      
      float dToPlayer = length(v1.pos.xz - _Player.xz);

      float dropoffVal = saturate((_FalloffRadius - dToPlayer)/_FalloffRadius);

      float fLength = (v1.debug.x/float(_NumVertsPerHair)) * dropoffVal;

      // We want to make sure that l isn't zero, 
      // or we will get some NaNs
      if( l > 0.001 ){
        difference = ( fLength - l ) / l;
      }

      // Here is where the actual verlet goes down!
      float3 translate = dif * .6 * difference;
      
      float3 t1 = 1 * translate;
      float3 t2 = 1 * translate;
      
      float3 n1 = v1.pos + t1;
      float3 n2 = v2.pos - t2;


  
      if( v1.uv.x == 0 ){
        v1.pos = v1.pos;
        v2.pos = n2 - t2;
      }else{
        // move towards
        v1.pos = n1;
        v2.pos = n2;
      }
        

      v1.debug.y = fLength;
      v2.debug.y = fLength;

              // Set both the values in the proper buffer location!
      _VertBuffer[ pID ] = v1;
      _VertBuffer[ pUp ] = v2;



    }



  }





}